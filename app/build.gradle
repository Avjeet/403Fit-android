apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'org.greenrobot.greendao'


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.ccloudapp.fit403"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix '-dev'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
            returnDefaultValues = true
        }

    }

    sourceSets { main { assets.srcDirs = ['src/main/assets'] } }
}

greendao {
    schemaVersion 1
}

dependencies {
    implementation 'com.android.support:multidex:1.0.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android support libraries
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibraryVersion}"
    implementation "com.android.support:support-v4:${rootProject.ext.supportLibraryVersion}"
    implementation "com.android.support:design:${rootProject.ext.supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${rootProject.ext.supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportLibraryVersion}"

    // AppIntro Library
    implementation "com.github.paolorotolo:appintro:${rootProject.ext.appintroVersion}"

    // Google location services
    implementation "com.google.android.gms:play-services-location:${rootProject.ext.playServicesVersion}"
    implementation "com.google.android.gms:play-services-places:${rootProject.ext.playServicesVersion}"

    // Firebase database
    implementation "com.google.firebase:firebase-database:${rootProject.ext.playServicesVersion}"

    //Parceler Library
    implementation "org.parceler:parceler-api:${rootProject.ext.parcelerVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor "org.parceler:parceler:${rootProject.ext.parcelerVersion}"

    // Glide image loading
    implementation "com.github.bumptech.glide:glide:${rootProject.ext.glideVersion}"

    // Circular Image View lib
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    // Joda Time lib
    implementation "net.danlew:android.joda:${rootProject.ext.jodatimeVersion}"

    // Evernote Job Scheduler Api
    implementation "com.evernote:android-job:${rootProject.ext.evernoteJobVersion}"

    //Event Bus
    implementation "org.greenrobot:eventbus:${rootProject.ext.eventBusVersion}"

    //Retrofit with OkHttp
    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.okhttp3:okhttp:${rootProject.ext.okhttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okhttpVersion}"

    //GreenDao Database
    implementation "org.greenrobot:greendao:${rootProject.ext.greendaoVersion}"

    //State ProgressBar
    implementation 'com.kofigyan.stateprogressbar:stateprogressbar:0.0.3'

    // Dagger
    implementation "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"

    // Butterknife
    implementation "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    // Rx Java and Android
    implementation "io.reactivex:rxandroid:${rootProject.ext.rxAndroidVersion}"
    implementation "io.reactivex:rxjava:${rootProject.ext.rxJavaVersion}"

    // Rx Relay
    implementation "com.jakewharton.rxrelay2:rxrelay:${rootProject.ext.rxRelayVersion}"

    implementation 'org.jacoco:org.jacoco.agent:0.7.5.201505241946'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'

    implementation 'net.zetetic:android-database-sqlcipher:3.5.6@aar'

    // Unit test dependencies
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.robolectricVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-core:${rootProject.ext.hamcrestVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${rootProject.ext.hamcrestVersion}"
    testImplementation "org.hamcrest:hamcrest-integration:${rootProject.ext.hamcrestVersion}"

    // Dagger-test
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"

    // Instrumentation tests dependencies
    androidTestImplementation "junit:junit:${rootProject.ext.junitVersion}"
    androidTestImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    androidTestImplementation "org.mockito:mockito-android:${rootProject.ext.mockitoVersion}"
    androidTestImplementation "com.android.support:support-annotations:${rootProject.ext.supportLibraryVersion}"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:${rootProject.ext.espressoVersion}", {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.espressoVersion}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.runnerRulesVersion}"
    androidTestImplementation "com.android.support.test:rules:${rootProject.ext.runnerRulesVersion}"
}
